# WiFi and MQTT
We now have two circles, it's time to draw the rest of the fucking owl.
In this part we will connect the ESP32 to a network via wifi and make it publish messages over MQTT.

>MQTT is an OASIS standard messaging protocol for the Internet of Things (IoT). It is designed as an extremely lightweight publish/subscribe messaging transport that is ideal for connecting remote devices with a small code footprint and minimal network bandwidth. MQTT today is used in a wide variety of industries, such as automotive, manufacturing, telecommunications, oil and gas, etc.

## Setting up WiFi
1. Open `src/main.cpp` and add the following include statement to the top: `#include <WiFi.h>`.
2. To be able to connect to a wifi network we need add the following lines to the `setup()` function:
```cpp
WiFi.mode(WIFI_STA);
WiFi.begin("SSID", "PASSWORD"); // replace with correct value

Serial.println("establishing wifi connection: ");

while (WiFi.status() != WL_CONNECTED) {
  Serial.print('.');
  delay(1000);
}

Serial.println(WiFi.localIP());
```


## Adding MQTT
1. To add MQTT support we need to include a library called `PubSubClient` to the project. Click `Libraries` under `QUICK ACCESS` in the PlatformIO menu and search for `PubSubClient`. Click on the one created by Nick O'Leary and click `Add to project`. Select your project in the dropdown and click `Add`.
2. Open `src/main.cpp` and add `#include <PubSubClient.h>` to the top.
3. Before `setup()` add the following lines:
```cpp
WiFiClient wifiClient;
PubSubClient mqttClient(wifiClient);
```

4. At the end of `setup()` add the following lines:
```cpp
mqttClient.setServer("IP_ADDRESS_OF_MQTT_HOST", 1883); // replace with the correct IP
mqttClient.connect("SOME_RANDOM_CLIENT_ID"); // replace with an arbitrary client id
```

5. At the beginning of `loop()` add `mqttClient.loop();`. This is required for when we start to subscribe to topics.

Before setting up subscription we should add support to publish the potentiometer value when the button is pressed. We could simply check if the button is pressed in the `loop()` and if so, publish a message.
Something in the lines of (do not add):
```cpp
if(buttonState == 1) {
  mqttClient.publish("some/random/topic", msg);
}
```

The problem with this is that for every loop the ESP would check the button state and if true it would publish a mqtt message. Pressing and holding the button for a brief moment would publish multiple messages.
Let's add a variable to make sure we only publish once per button press.

6. Before `setup()` add the line `int lastButtonState;`
7. At the end of `loop()` add the following lines:
```cpp
if(buttonState == 1 && buttonState != lastButtonState) {
  mqttClient.publish("some/random/topic", msg); // replace with an arbitrary topic name. it should be easily identified
}

lastButtonState = buttonState;
```

Your code should look something like this now:
```cpp
#include <Arduino.h>
#include <TFT_eSPI.h>
#include <WiFi.h>
#include <PubSubClient.h>

#define BUTTON_PIN 26
#define POTENTIOMETER_PIN 33

TFT_eSPI tft = TFT_eSPI();
char previousDisplayValue[100];
WiFiClient wifiClient;
PubSubClient mqttClient(wifiClient);
int lastButtonState;

void setup() {
  Serial.begin(115200);

  pinMode(BUTTON_PIN, INPUT);
  tft.init();
  tft.setRotation(1);

  WiFi.mode(WIFI_STA);
  WiFi.begin("SSID", "PASSWORD");

  Serial.println("establishing wifi connection: ");

  while (WiFi.status() != WL_CONNECTED) {
    Serial.print('.');
    delay(1000);
  }

  Serial.println(WiFi.localIP());

  mqttClient.setServer("MQTT_SERVER_IP", 1883);
  mqttClient.connect("CLIENT_ID");
}

void printStuff(char* someText) {
  if (strcmp(previousDisplayValue, someText) == 0) {
    return;
  }

  tft.fillScreen(TFT_BLACK);
  tft.setCursor(0, 0, 2);
  tft.setTextColor(TFT_RED,TFT_BLACK);
  tft.setTextFont(2);
  tft.println(someText);

  strcpy(previousDisplayValue, someText);
}

void loop() {
  mqttClient.loop();

  int buttonState = digitalRead(BUTTON_PIN);
  int potentiometerValue = analogRead(POTENTIOMETER_PIN);
  int mappedValue = map(potentiometerValue, 0, 4095, 0, 10);

  char msg[4];
  itoa(mappedValue, msg, 10);

  printStuff(msg);

  if(buttonState == 1 && buttonState != lastButtonState) {
    mqttClient.publish("some/random/topic", msg);
  }

  lastButtonState = buttonState;
}
```

Open your favorite MQTT exploring tool on your laptop and connect to the same mqtt server. I normally use [MQTT-Explorer](https://github.com/thomasnordquist/MQTT-Explorer). 
You should be able to see the messages that you publish to the topic specified at `mqttClient.publish("some/random/topic", msg);` when you press the button.


Let's add some feedback on the display when we publish a message. 

8. Add a second argument to the `printStuff()` function: `printStuff(char* someText, int durationInMS)`.
9. At the end of the function add `delay(durationInMS);`. The `delay()` will pause the thread for the given time specified in milliseconds.
10. Update the `printStuff(msg)` call to include `0` as the second parameter.
11. In the button pressed check add:
```cpp  
char publishMessageInformation[] = "Published MQTT message";
printStuff(publishMessageInformation, 3000);
```

12. Upload the code and try sending a message by pressing the button. The text "Published MQTT message" should now be displayed for 3 seconds.

The problem with using `delay()` is that it stops the entire program execution. This is generally an undesired behaviour. The recommended way to deal with timings is to use the built in function `millis()`:
>millis() Returns the number of milliseconds passed since the Arduino board began running the current program. This number will overflow (go back to zero), after approximately 50 days.

13. To visualize the problem with `delay()` add the line `Serial.println(msg);` to the end of the `loop()` function. Upload and open the serial monitor. 
There should be a steady stream of values being printed but if you click the button the stream will stop for a moment because of the `delay()` call.

14. Let's fix this by using `millis()`. Add the following two lines before `setup()`
```cpp
unsigned int previousPrintStuffTimestamp;
int previousPrintStuffDuration;
```

These variables will keep track of the previous call to `printStuff()` so that the ESP know when it's ok to show a different text on the display.

15. Update `printStuff()` to this:
```cpp
void printStuff(char* someText, int durationInMS) {
  // These two if-statement could be merged into one but let's keep them separated for now.
  if (strcmp(previousDisplayValue, someText) == 0) { 
    return;
  }

  if (millis() - previousPrintStuffTimestamp < previousPrintStuffDuration) {
    return;
  }

  tft.fillScreen(TFT_BLACK);
  tft.setCursor(0, 0, 2);
  tft.setTextColor(TFT_RED,TFT_BLACK);
  tft.setTextFont(2);
  tft.println(someText);

  strcpy(previousDisplayValue, someText);

  previousPrintStuffTimestamp = millis();
  previousPrintStuffDuration = durationInMS;
}
```

This adds the behaviour to respect text display duration of the previous call to `printStuff()`. Upload the code and verify that the stream in the serial port continues even after the button has been pressed.

You code should now look something like this:

```cpp
#include <Arduino.h>
#include <TFT_eSPI.h>
#include <WiFi.h>
#include <PubSubClient.h>

#define BUTTON_PIN 26
#define POTENTIOMETER_PIN 33

TFT_eSPI tft = TFT_eSPI();
char previousDisplayValue[100];
WiFiClient wifiClient;
PubSubClient mqttClient(wifiClient);
int lastButtonState;
unsigned int previousPrintStuffTimestamp;
int previousPrintStuffDuration;

void setup() {
  Serial.begin(115200);

  pinMode(BUTTON_PIN, INPUT);
  tft.init();
  tft.setRotation(1);

  WiFi.mode(WIFI_STA);
  WiFi.begin("SSID", "PASSWORD");

  Serial.println("establishing wifi connection: ");

  while (WiFi.status() != WL_CONNECTED) {
    Serial.print('.');
    delay(1000);
  }

  Serial.println(WiFi.localIP());

  mqttClient.setServer("MQTT_SERVER_IP", 1883);
  mqttClient.connect("CLIENT_ID");
}

void printStuff(char* someText, int durationInMS) {
  if (strcmp(previousDisplayValue, someText) == 0) {
    return;
  }

  if (millis() - previousPrintStuffTimestamp < previousPrintStuffDuration) {
    return;
  }

  tft.fillScreen(TFT_BLACK);
  tft.setCursor(0, 0, 2);
  tft.setTextColor(TFT_RED,TFT_BLACK);
  tft.setTextFont(2);
  tft.println(someText);

  strcpy(previousDisplayValue, someText);

  previousPrintStuffTimestamp = millis();
  previousPrintStuffDuration = durationInMS;
}

void loop() {
  mqttClient.loop();

  int buttonState = digitalRead(BUTTON_PIN);
  int potentiometerValue = analogRead(POTENTIOMETER_PIN);
  int mappedValue = map(potentiometerValue, 0, 4095, 0, 10);

  char msg[4];
  itoa(mappedValue, msg, 10);

  printStuff(msg, 0);

  if(buttonState == 1 && buttonState != lastButtonState) {
    mqttClient.publish("some/random/topic", msg);
    
    char publishMessageInformation[] = "Published MQTT message";
    printStuff(publishMessageInformation, 3000);
  }

  lastButtonState = buttonState;

  Serial.println(msg);
}
```

## Extra
Find someone else to try listening for MQTT messages using the code below:
```cpp

```